print "Hello World!";

var a = "global";
print a;

print "Entering Block";
{
    print a;
    a = a + "!";
}
print "Leaving Block";

print "Entering Block";
{
    var a = "local";
    print a;
}
print "Leaving Block";

print a;

var val = "if";
if (val == "if" and 23 == 2) {
    print "In If.";
} else {
    print "In Else.";
}

print "hi" and 3;

var a = 0;
var b = 1;
print "Printing some Fibonacci numbers";
while (a < 10000) {
  print a;
  var temp = a;
  a = b;
  b = temp + b;
}

for (var i = 0; i < 10; i = i + 1) {
    print i;
}

fun hello(name) {
    print "Hello " + name + "!";
}

hello("Suhas");

fun retHello() {
    return "Hello World!";
}

print retHello();

fun fibonacci(n) {
    if (n <= 1) {
        return 1;
    } else {
        return fibonacci(n - 1) + fibonacci(n - 2);
    }
}

for (var i = 0; i < 20; i = i + 1) {
  print fibonacci(i);
}

fun makeCounter() {
    var i = 0;
    fun count() {
        i = i + 1;
        print i;
    }

    return count;
}

var count = makeCounter();

for (var i = 0; i < 10; i = i + 1) {
    count();
}

fun walk() {
    print "Walking!";
}

var move = walk;
move();

class Dog {
    bark() {
        print "Bark!";
    }

    getDogProvider() {
        fun dogProvider() {
            return this;
        }

        return dogProvider;
    }
}

print Dog;

var dog = Dog();
print dog;

dog.howl = "Howl!";

print dog.howl;
dog.bark();

var dogProvider = dog.getDogProvider();

print dogProvider();

class Rectangle {
    init(width, height) {
        this.width = width;
        this.height = height;
    }

    area() {
        return this.width * this.height;
    }
}

var rectangle = Rectangle(4, 1210);
print "Rectangle";
print rectangle.area();

class Square < Rectangle {
    init(dim) {
        super.init(dim, dim);
    }
}

var square = Square(5);
print "Printing area of a square.";
print square.area();


